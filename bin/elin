#!/usr/bin/env bb

#_{:clj-kondo/ignore [:namespace-name-mismatch]}
(ns elin
  (:require
    [clojure.pprint :as pp]
    [babashka.cli :as cli]
    [babashka.deps :as deps]
    [clojure.java.io :as io]
    [clojure.string :as str]))

(def ^:private cwd (System/getProperty "user.dir"))

(def ^:private project-root
  (-> (io/file *file*)
      (.getParentFile)
      (.getParentFile)
      (.getAbsolutePath)))

(deps/add-deps
  {:deps {'liquidz/elin {:local/root project-root}}})

(require
  '[babashka.process :as b.process]
  '[elin.config :as e.config]
  '[elin.function.jack-in :as e.f.jack-in]
  '[elin.util.nrepl :as e.u.nrepl])

(def ^:private global-cli-options
   {:help {:default false :coerce :boolean}})

(def ^:private cli-options-map
  {"repl" ^{:doc "Start a REPL session in the current project."}
   {:force-leiningen {:coerce :boolean}
    :force-shadow-cljs {:coerce :boolean}
    :dry-run {:coerce :boolean}
    :port {:coerce :long :validate pos?}
    :instant {:coerce :boolean}
    :help {:default false :coerce :boolean}}

   "lint" ^{:doc "Lint the elin configuration in the current project."}
   {:help {:default false :coerce :boolean}}})

(def ^:private elin-option-map
  (update-vals cli-options-map #(->> (keys %)
                                     (map (comp (partial str "--") name))
                                     (set))))

(defn- parse-opts
  [[sub-command & args]]
  (let [option-set (or (get elin-option-map sub-command)
                       #{})
        cli-options (or (get cli-options-map sub-command)
                        {})
        cli-option? #(boolean (some (fn [option]
                                      (str/starts-with? % option))
                                    option-set))
        {elin-args true rest-args false} (group-by cli-option? args)]
    {:options (cli/parse-opts elin-args cli-options)
     :rest-args rest-args}))

(defn- show-help
  [sub-command]
  (let [spec (get cli-options-map sub-command)]
    (cli/format-opts (merge {:spec spec}
                            {:order (vec (keys spec))}))))

(defn- show-available-sub-commands
  []
  (str "Available sub-commands:\n"
       (->> cli-options-map
           (map (fn [[cmd options]]
                  (let [desc (or (:doc (meta options)) "")]
                    (format "  %s\t%s" cmd desc))))
           (str/join "\n"))))

(defmulti dispatch first)
(defmethod dispatch :default
  [args]
  (let [options (cli/parse-opts args global-cli-options)]
    (if (:help options)
      (println (show-available-sub-commands))
      (throw (ex-info "Unknown command" {})))))

(defmethod dispatch "lint"
  [args]
  (let [dir (System/getProperty "user.dir")
        sub-command (first args)
        {:keys [options]} (parse-opts args)]
    (if (:help options)
      (println (show-help sub-command))
      (if-let [result (e.config/lint-config dir)]
        (do
          (doseq [[root-key errors] result]
            (println (format "### %s" (name root-key)))
            (->> errors
                 (mapcat (fn [[k vs]]
                           (map #(hash-map :key k :error %) vs)))
                 (pp/print-table))
            (println ""))
          (System/exit 1))
        (println "Configuration lint passed.")))))

(defmethod dispatch "repl"
  [args]
  (let [sub-command (first args)
        {:keys [options rest-args]} (parse-opts args)]
    (if (:help options)
      (println (show-help sub-command))
      (let [;; TODO apply options to force protject-type
            [project-type _] (e.f.jack-in/select-project {} cwd)
            port (or (:port options)
                     (e.u.nrepl/get-free-port))
            commands (:command (e.f.jack-in/generate-command project-type port rest-args))]
        (if (:dry-run options)
          (println (str/join " " commands))
          (apply b.process/shell {:out :inherit :in :inherit} commands))))))

(defn -main [& args]
  (try
    (dispatch args)
    (catch Exception ex
      (binding [*out* *err*]
        (println (ex-message ex))))))

(when (= *file* (System/getProperty "babashka.file"))
  (apply -main *command-line-args*))

(comment
  (let [sample-args ["repl" "-A:dev" "--dry-run"]]
    (apply -main sample-args)))

;; vim:ft=clojure
